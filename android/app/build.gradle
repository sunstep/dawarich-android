plugins {
    id "com.android.application"
    id "org.jetbrains.kotlin.android"
    id "dev.flutter.flutter-gradle-plugin"
}

dependencies {
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:2.1.5'
    implementation "androidx.window:window:1.4.0"
    implementation "androidx.window:window-java:1.4.0"
}

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    namespace = "com.sunstep.dawarich"
    compileSdk = 36

    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_21
        targetCompatibility JavaVersion.VERSION_21
    }

    kotlinOptions {
        jvmTarget = "21"
    }

    kotlin {
        jvmToolchain(21)
    }

    defaultConfig {
        applicationId = "com.sunstep.dawarich"
        // You can update the following values to match your application needs.
        // For more information, see: https://flutter.dev/to/review-gradle-config.
        minSdk = flutter.minSdkVersion
        targetSdk = 36
        versionCode = flutter.versionCode
        versionName = flutter.versionName
    }

    signingConfigs {
        debug {
            // Keep debug signing stable across rebuilds/machines.
            // This matches Flutter/Gradle defaults and ensures Android Keystore-backed
            // data (e.g., flutter_secure_storage encryptedSharedPreferences) stays readable.
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
            storeType     'pkcs12'
        }
        release {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
            storeType     'pkcs12'
        }
    }

    buildTypes {
        release {
            // attach the signing config
            signingConfig signingConfigs.release

            // Make sure release label stays clean.
            resValue "string", "app_name", "Dawarich"

            // optimizations
            minifyEnabled    true
            shrinkResources  true

            // ProGuard/R8 rules
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'),
                    'proguard-rules.pro'

            debuggable       false
            jniDebuggable    false
        }

        debug {
            // Install debug build alongside release.
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"

            // Use stable debug signing. Helps prevent "lost" secure storage on reinstall.
            signingConfig signingConfigs.debug

            // Distinct launcher name.
            resValue "string", "app_name", "Dawarich (Dev)"
        }
    }

    flavorDimensions += "distribution"

    productFlavors {
        gms {
            dimension "distribution"

            versionNameSuffix "-gms"
        }
        foss {
            dimension "distribution"

            proguardFiles 'proguard-foss-rules.pro'
            applicationIdSuffix ".foss"
            versionNameSuffix "-foss"
        }
    }

    buildFeatures {
        buildConfig true
    }
}

// Exclude Google Play Services from FOSS builds
configurations.matching { it.name.toLowerCase().contains("foss") }.configureEach {
    exclude group: "com.google.android.gms"
}

flutter {
    source = "../.."
}

